plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
    id 'maven-publish'
}

apply from: "$project.rootDir/scripts/appcenter.gradle"
apply from: "$project.rootDir/scripts/jacoco.gradle"

android {
    namespace 'com.mvi.app' //for unique R class, this is like a groupId for artifact
    compileSdk 34
    ndkVersion '25.2.9519653'

    defaultConfig {
        applicationId "com.mvi.app"
        minSdk 21
        targetSdk 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }

    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion "1.4.6"
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    signingConfigs {

        debug {
            storeFile file("../keystore.keystore")
            storePassword "Nepal@123!!"
            keyAlias "app"
            keyPassword "Nepal@123!!"
        }

        release {
            storeFile file("../keystore.keystore")
            storePassword "Nepal@123!!"
            keyAlias "app"
            keyPassword "Nepal@123!!"
        }

    }

    buildTypes {

        debug {
            debuggable true
            jniDebuggable true
            minifyEnabled false
            shrinkResources false

            matchingFallbacks = ['debug']
            signingConfig signingConfigs.debug
            //testCoverageEnabled true //already enabled in jacoco.gradle
            applicationIdSuffix '.debug'
            versionNameSuffix ".${getBuildNumber()}"
        }

        release {
            debuggable false
            jniDebuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            matchingFallbacks = ['release']
            signingConfig signingConfigs.release
            versionNameSuffix ".${getBuildNumber()}"
        }

        enterpriseQa {
            initWith debug //copies everything from debug buildTypes
            renderscriptDebuggable true
            renderscriptOptimLevel 3
            applicationIdSuffix '.enterpriseQa'
        }

    }

    flavorDimensions "prod"
    productFlavors {

        retail {
            applicationIdSuffix ".retail"
            dimension "prod"
            versionCode 151010000
            versionName "1.0.0"
            signingConfig signingConfigs.release

            //we can define data for different product flavors
            manifestPlaceholders = [ google_map_key:"retail_map_key",
                                     app_label_name:"retail_map_label" ]

            //Gradle generates a BuildConfig class that contains static configuration constants that are specific to the build at build time.
            buildConfigField "String", "SERVER_URL", "\"http://retail.myserver.com\""

            resValue "string", "base_url", "\"http://retail.myserver.com\""
        }

        free {
            applicationIdSuffix ".free"
            dimension "prod"
            versionCode 151010000
            versionName "1.0.0"
            signingConfig signingConfigs.release

            manifestPlaceholders = [ google_map_key:"free_map_key",
                                     app_label_name:"free_map_label" ]

            //Gradle generates a BuildConfig class that contains static configuration constants that are specific to the build at build time.
            buildConfigField "String", "SERVER_URL", "\"http://free.myserver.com\""

            resValue "string", "base_url", "\"http://free.myserver.com\""
        }

    }

    sourceSets {

        main {
            manifest.srcFile 'src/main/AndroidManifest.xml'
            java.srcDirs = ['src/main/java']
            kotlin.srcDirs += 'src/main/myKotlin'
            res.srcDirs = ['src/main/res']
            assets.srcDirs = ['src/main/assets']
        }

        retailRelease {
            manifest.srcFile 'src/retailRelease/AndroidManifest.xml'
            java.srcDirs = ['src/retailRelease/java']
            kotlin.srcDirs += 'src/retailRelease/myKotlin'
            res.srcDirs = ['src/retailRelease/res']
            assets.srcDirs = ['src/retailRelease/assets']
        }

        retailDebug {
            manifest.srcFile 'src/retailDebug/AndroidManifest.xml'
            java.srcDirs = ['src/retailDebug/java']
            kotlin.srcDirs += 'src/retailDebug/myKotlin'
            res.srcDirs = ['src/retailDebug/res']
            assets.srcDirs = ['src/retailDebug/assets']
        }

        retailEnterpriseQa {
            manifest.srcFile 'src/retailEnterpriseQa/AndroidManifest.xml'
            java.srcDirs = ['src/retailEnterpriseQa/java']
            kotlin.srcDirs += 'src/retailEnterpriseQa/myKotlin'
            res.srcDirs = ['src/retailEnterpriseQa/res']
            assets.srcDirs = ['src/retailEnterpriseQa/assets']
        }

    }

}


dependencies {
    implementation fileTree(include: '*.jar', dir: 'libs')
    implementation fileTree(include: '*.jar', dir: 'ext_lib')

    //implementation(name:'agemodule-release', ext:'aar')
    implementation project(path: ":agemodule") //renamed in settings.gradle

    implementation "androidx.core:core-ktx:$kotlin_version"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'
    implementation 'androidx.activity:activity-compose:1.7.1'
    implementation "androidx.compose.ui:ui:$compose_ui_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_ui_version"
    implementation "androidx.compose.material:material:$compose_ui_version"
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_ui_version"
    implementation "androidx.compose.runtime:runtime-livedata:$compose_ui_version"

    //timber
    implementation 'com.jakewharton.timber:timber:5.0.1'

    //reactivex
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"
    implementation "androidx.lifecycle:lifecycle-reactivestreams-ktx:2.6.1"
    implementation "androidx.lifecycle:lifecycle-reactivestreams:2.6.1"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-reactivestreams:2.6.1"

    //dagger hilt
    implementation "com.google.dagger:hilt-android:2.46"
    kapt "com.google.dagger:hilt-compiler:2.46"
    implementation 'com.google.dagger:hilt-android:2.46'
    kapt 'androidx.hilt:hilt-compiler:1.0.0'
    kapt 'com.google.dagger:hilt-android-compiler:2.37'

    //AppCenter
    def appCenterSdkVersion = '4.4.5'
    implementation "com.microsoft.appcenter:appcenter-analytics:${appCenterSdkVersion}"
    implementation "com.microsoft.appcenter:appcenter-crashes:${appCenterSdkVersion}"
}